<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="TcLog" Id="{70fb8269-5067-405b-9fff-7631f944e2d4}" SpecialFunc="None">
    <Declaration><![CDATA[/// `TcLog` is the client side of the logger.  
///
/// It is used to send the logs to `TcLogCore`. Typically, it is instantiated
/// once in each `FUNCTION_BLOCK` or `PROGRAM` where logging is done. The same 
/// instance can then be used for all logging within that POU. 
///
/// `TcLog` implements a string builder that facilitates the compilation of the log
/// message without the need for external data type conversions or multiple chained `CONCAT` calls.
/// See the method section for more information.
///
/// The last method to call after the log message has been built is one of ´[Debug|Information|Warning|Error|Fatal]`. 
/// This sets the log level of the message and sends it off to the log message queue of `TcLogCore` where it will
/// be processed according to its configuration.
{attribute 'linkalways'}
{attribute 'hide_all_locals'}
{attribute 'reflection'} 
FUNCTION_BLOCK TcLog IMPLEMENTS ILog
VAR
  _logConditionSatisfied : BOOL; // flag that indicates, whether log condition has been satisfied.
                                // Logging will only be carried out it this evaluates to TRUE.                    
  _logDataInitialized : BOOL; // flag that indicate, whether log data has been initialized.
                             // Necessary for configuration methods to provide a fluent interface.                            
  _logData : Tc2_System.T_MaxString; // contains message to be logged.
  _usedLogger : ILogCore;
  {attribute 'instance-path'} 
  {attribute 'noinit'} 
  _instancePath : STRING; // contains instance path
END_VAR
VAR_STAT
  {attribute 'hide'}
  s_loggerSingletonInstance : ILogCore; // Wires up TcLog ("Client") to TcLogCore ("Server") (Singleton-Behaviour)
END_VAR
VAR CONSTANT
  InvalidLoggerReference : UDINT := 0;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Appenders" Id="{bab4bfce-302e-4e7a-b43b-c75063ccccb2}">
      <Folder Name="Dates" Id="{4044c0c4-d771-475a-bbac-f743882eadcf}" />
      <Folder Name="Numbers" Id="{d055dde0-2985-4af4-895c-028422d08ada}" />
    </Folder>
    <Method Name="AppendAny" Id="{13a72111-b34b-43eb-9dd9-69ff4503fdca}" FolderPath="Appenders\">
      <Declaration><![CDATA[/// Appends any given data to the log string. The type of the provided data
/// has to be one of the [standard data types](https://infosys.beckhoff.com/english.php?content=../content/1033/tc3_plc_intro/2529388939.html&id=).
/// This method can be chained.
METHOD AppendAny : REFERENCE TO TcLog
VAR_INPUT
  /// data to be appended to log string.
  data : ANY;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[InitializeLogData();

_logData := Tc2_Standard.CONCAT(_logData, AnyToString(data));

AppendAny REF= THIS^;]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendByte" Id="{e8dec6d7-635f-4a83-9c23-55cec7008389}" FolderPath="Appenders\Numbers\">
      <Declaration><![CDATA[/// Appends a BYTE to the log string.
/// This method can be chained.
METHOD PUBLIC AppendByte : REFERENCE TO TcLog
VAR_INPUT
  /// data to be appended to log string.
  data : BYTE;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendByte REF= AppendString(TO_STRING(data));]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendDate" Id="{5db37b6b-91bb-4dd2-8c59-f23a5c1fad9a}" FolderPath="Appenders\Dates\">
      <Declaration><![CDATA[/// Appends a DATE to the log string.
/// This method can be chained.
METHOD PUBLIC AppendDate : REFERENCE TO TcLog
VAR_INPUT
  /// data to be appended to log string.
  data : DATE;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendDate REF= AppendString(TO_STRING(data));]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendDateAndTime" Id="{59bd6f39-873b-4ab9-ab97-648c9532afc1}" FolderPath="Appenders\Dates\">
      <Declaration><![CDATA[/// Appends a DATE_AND_TIME to the log string.
/// This method can be chained.
METHOD PUBLIC AppendDateAndTime : REFERENCE TO TcLog
VAR_INPUT
  /// data to be appended to log string.
  data : DATE_AND_TIME;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendDateAndTime REF= AppendString(TO_STRING(data));]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendDint" Id="{17af44e7-bb10-40af-848d-87377a1a8882}" FolderPath="Appenders\Numbers\">
      <Declaration><![CDATA[/// Appends a DINT to the log string.
/// This method can be chained.
METHOD PUBLIC AppendDint : REFERENCE TO TcLog
VAR_INPUT
  /// data to be appended to log string.
  data : DINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendDint REF= AppendString(TO_STRING(data));]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendDword" Id="{04d8651b-2b7f-4b42-946f-b35fdb60d4ab}" FolderPath="Appenders\Numbers\">
      <Declaration><![CDATA[/// Appends a DWORD to the log string.
/// This method can be chained.
METHOD PUBLIC AppendDword : REFERENCE TO TcLog
VAR_INPUT
  /// data to be appended to log string.
  data : DWORD;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendDword REF= AppendString(TO_STRING(data));]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendInt" Id="{a89412c9-8947-4735-b889-eb67b15f95ab}" FolderPath="Appenders\Numbers\">
      <Declaration><![CDATA[/// Appends an INT to the log string.
/// This method can be chained.
METHOD PUBLIC AppendInt : REFERENCE TO TcLog
VAR_INPUT
  /// data to be appended to log string.
  data : INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendInt REF= AppendString(TO_STRING(data));]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendLDate" Id="{886a7c4c-f71d-4bda-9de3-6455919bc0a9}" FolderPath="Appenders\Dates\">
      <Declaration><![CDATA[/// Appends a LDATE to the log string.
/// This method can be chained.
METHOD PUBLIC AppendLDate : REFERENCE TO TcLog
VAR_INPUT
  /// data to be appended to log string.
  data : LDATE;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendLDate REF= AppendString(TO_STRING(data));]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendLDateAndTime" Id="{96ec1678-12e3-45cf-b176-f73cf065d41c}" FolderPath="Appenders\Dates\">
      <Declaration><![CDATA[/// Appends a LDATE_AND_TIME to the log string.
/// This method can be chained.
METHOD PUBLIC AppendLDateAndTime : REFERENCE TO TcLog
VAR_INPUT
  /// data to be appended to log string.
  data : LDATE_AND_TIME;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendLDateAndTime REF= AppendString(TO_STRING(data));]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendLint" Id="{8701a1f5-326d-4876-95ab-f103b1064cfd}" FolderPath="Appenders\Numbers\">
      <Declaration><![CDATA[/// Appends an LINT to the log string.
/// This method can be chained.
METHOD PUBLIC AppendLint : REFERENCE TO TcLog
VAR_INPUT
  /// data to be appended to log string.
  data : LINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendLint REF= AppendString(TO_STRING(data));]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendLreal" Id="{2a586062-f9ce-4c51-a38b-fa1bb07e5e5c}" FolderPath="Appenders\Numbers\">
      <Declaration><![CDATA[/// Appends an LREAL to the log string.
/// This method can be chained.
METHOD PUBLIC AppendLreal : REFERENCE TO TcLog
VAR_INPUT
  /// data to be appended to log string.
  data : LREAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendLreal REF= AppendString(TO_STRING(data));]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendLTimeOfDay" Id="{4c37d249-0f30-47bc-b661-4345dfc31781}" FolderPath="Appenders\Dates\">
      <Declaration><![CDATA[/// Appends a LTIME_OF_DAY to the log string.
/// This method can be chained.
METHOD PUBLIC AppendLTimeOfDay : REFERENCE TO TcLog
VAR_INPUT
  /// data to be appended to log string.
  data : LTIME_OF_DAY;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendLTimeOfDay REF= AppendString(TO_STRING(data));]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendLword" Id="{4c77ad1e-01af-4267-a533-17dfdedc897c}" FolderPath="Appenders\Numbers\">
      <Declaration><![CDATA[/// Appends an LWORD to the log string.
/// This method can be chained.
METHOD PUBLIC AppendLword : REFERENCE TO TcLog
VAR_INPUT
  /// data to be appended to log string.
  data : LWORD;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendLword REF= AppendString(TO_STRING(data));]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendReal" Id="{3a5cc81f-e994-4ad8-95af-72d9fb9ec4fc}" FolderPath="Appenders\Numbers\">
      <Declaration><![CDATA[/// Appends a REAL to the log string.
/// This method can be chained.
METHOD PUBLIC AppendReal : REFERENCE TO TcLog
VAR_INPUT
  /// data to be appended to log string.
  data : REAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendReal REF= AppendString(TO_STRING(data));]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendSint" Id="{fc73aa88-61d5-4883-824a-c189137d8398}" FolderPath="Appenders\Numbers\">
      <Declaration><![CDATA[/// Appends a SINT to the log string.
/// This method can be chained.
METHOD PUBLIC AppendSint : REFERENCE TO TcLog
VAR_INPUT
  /// data to be appended to log string.
  data : SINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendSint REF= AppendString(TO_STRING(data));]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendString" Id="{7aeade46-e7f2-45b6-b46c-fff144444876}" FolderPath="Appenders\">
      <Declaration><![CDATA[/// Appends another string to the log message.
/// This method can be chained.
METHOD AppendString : REFERENCE TO TcLog
VAR_INPUT
  /// data to be appended to log string.
  data : Tc2_System.T_MaxString; 
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[InitializeLogData();

_logData := Tc2_Standard.CONCAT(_logData, data);
  
AppendString REF= THIS^;]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendTimeOfDay" Id="{f76f51fd-1bd2-428f-8692-abf7422da3f7}" FolderPath="Appenders\Dates\">
      <Declaration><![CDATA[/// Appends a TIME_OF_DAY to the log string.
/// This method can be chained.
METHOD PUBLIC AppendTimeOfDay : REFERENCE TO TcLog
VAR_INPUT
  /// data to be appended to log string.
  data : TIME_OF_DAY;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendTimeOfDay REF= AppendString(TO_STRING(data));]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendUdint" Id="{d7cb9751-25fb-4666-8d1a-02ce5cd31600}" FolderPath="Appenders\Numbers\">
      <Declaration><![CDATA[/// Appends a UDINT to the log string.
/// This method can be chained.
METHOD PUBLIC AppendUdint : REFERENCE TO TcLog
VAR_INPUT
  /// data to be appended to log string.
  data : UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendUdint REF= AppendString(TO_STRING(data));]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendUint" Id="{8fb9db58-9211-4179-9ed0-a5b7c29461e9}" FolderPath="Appenders\Numbers\">
      <Declaration><![CDATA[/// Appends a UINT to the log string.
/// This method can be chained.
METHOD PUBLIC AppendUint : REFERENCE TO TcLog
VAR_INPUT
  /// data to be appended to log string.
  data : UINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendUint REF= AppendString(TO_STRING(data));]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendUlint" Id="{3328adca-4c1a-4c7e-8a04-12bc8cf451fd}" FolderPath="Appenders\Numbers\">
      <Declaration><![CDATA[/// Appends a ULINT to the log string.
/// This method can be chained.
METHOD PUBLIC AppendUlint : REFERENCE TO TcLog
VAR_INPUT
  /// data to be appended to log string.
  data : ULINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendUlint REF= AppendString(TO_STRING(data));]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendUsint" Id="{49fc8f23-ea56-4a64-9709-514809a66de7}" FolderPath="Appenders\Numbers\">
      <Declaration><![CDATA[/// Appends a USINT to the log string.
/// This method can be chained.
METHOD PUBLIC AppendUsint : REFERENCE TO TcLog
VAR_INPUT
  /// data to be appended to log string.
  data : USINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendUsint REF= AppendString(TO_STRING(data));]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendVariable" Id="{67339878-c4f6-4555-96b1-10fb6c84bab0}" FolderPath="Appenders\">
      <Declaration><![CDATA[/// Appends information about the given variable to the log string. 
/// This method can be chained.
/// Example: 

/// ```st
/// VAR
///   Logger: TcLog;
///    myInt : INT := 10;
///   myVarInfo : __SYSTEM.VAR_INFO := __VARINFO(myInt);
/// END_VAR
/// Logger.AppendVariable(myVarInfo, myInt);
/// ```
///
METHOD AppendVariable : REFERENCE TO TcLog
VAR_INPUT
  /// [VAR_INFO](https://infosys.beckhoff.com/index.php?content=../content/1031/tc3_plc_intro/3527777675.html&id=) of variable to be appended to log string.
  varInfo : __SYSTEM.VAR_INFO;
  /// Value of variable to be appended to log string.
  value : ANY; 
END_VAR
VAR
  format : Tc2_Utilities.FB_FormatString;
  symbolName : Tc2_System.T_MaxString;
  typeName : Tc2_System.T_MaxString;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[InitializeLogData();

symbolName := varInfo.Symbol;
typeName := varInfo.TypeName; 

format(
  sFormat := '%s [%s] : ',
  arg1 := Tc2_Utilities.F_String(symbolName),
  arg2 := Tc2_Utilities.F_String(typeName));
  
AppendString(format.sOut);
AppendAny(value);

AppendVariable REF= THIS^;]]></ST>
      </Implementation>
    </Method>
    <Method Name="AppendWord" Id="{12f3ad43-8427-457b-a037-4c92b68dae39}" FolderPath="Appenders\Numbers\">
      <Declaration><![CDATA[/// Appends a WORD to the log string.
/// This method can be chained.
METHOD PUBLIC AppendWord : REFERENCE TO TcLog
VAR_INPUT
  /// data to be appended to log string.
  data : WORD;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[AppendWord REF= AppendString(TO_STRING(data));]]></ST>
      </Implementation>
    </Method>
    <Method Name="Debug" Id="{a6ab730f-dade-4a97-bd0b-d6237fbafb52}">
      <Declaration><![CDATA[/// Logs the log string in DEBUG level.
METHOD Debug : BOOL
VAR_INPUT
  /// Message to be appended to log string.
  message : Tc2_System.T_MaxString; 
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[InitializeLogData();
IF _logConditionSatisfied THEN
  IF _usedLogger <> InvalidLoggerReference THEN 
    _usedLogger.LogStandardFormat(Tc2_Standard.CONCAT(_logData, message), ShortenInstancePath(_instancePath), LogLevels.Debug); 
  ELSE
    Tc2_System.ADSLOGSTR(ADSLOG_MSGTYPE_LOG OR LogLevelToAdsLogMsgType(LogLevels.Error), 'usedLogger: No valid reference', ''); 
  END_IF
END_IF
_logDataInitialized := FALSE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Error" Id="{33e3e66d-5512-4aeb-a63d-1183d4d9acad}">
      <Declaration><![CDATA[/// Logs the log string in ERROR level.
METHOD Error : BOOL
VAR_INPUT
  /// Message to be appended to log string.
  message : Tc2_System.T_MaxString; 
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[InitializeLogData();
IF _logConditionSatisfied THEN
  IF _usedLogger <> InvalidLoggerReference THEN 
    _usedLogger.LogStandardFormat(Tc2_Standard.CONCAT(_logData, message), ShortenInstancePath(_instancePath), LogLevels.Error); 
  ELSE
    Tc2_System.ADSLOGSTR(ADSLOG_MSGTYPE_LOG OR LogLevelToAdsLogMsgType(LogLevels.Error), 'usedLogger: No valid reference', ''); 
  END_IF
END_IF
_logDataInitialized := FALSE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Fatal" Id="{4817ad2c-00f3-4ea2-9126-d711c8897cf3}">
      <Declaration><![CDATA[/// Logs the log string in FATAL level.
METHOD Fatal : BOOL
VAR_INPUT
  /// Message to be appended to log string.
  message : Tc2_System.T_MaxString; 
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[InitializeLogData();
IF _logConditionSatisfied THEN
  IF _usedLogger <> InvalidLoggerReference THEN 
    _usedLogger.LogStandardFormat(Tc2_Standard.CONCAT(_logData, message), ShortenInstancePath(_instancePath), LogLevels.Fatal); 
  ELSE
    Tc2_System.ADSLOGSTR(ADSLOG_MSGTYPE_LOG OR LogLevelToAdsLogMsgType(LogLevels.Error), 'usedLogger: No valid reference', ''); 
  END_IF
END_IF
_logDataInitialized := FALSE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Information" Id="{ab474f28-2e54-4276-8fa1-5987c3e94a5d}">
      <Declaration><![CDATA[/// Logs the log string in INFORMATION level.
METHOD Information : BOOL
VAR_INPUT
  /// Message to be appended to log string.
  message : Tc2_System.T_MaxString; 
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[InitializeLogData();
IF _logConditionSatisfied THEN
  IF _usedLogger <> InvalidLoggerReference THEN 
    _usedLogger.LogStandardFormat(Tc2_Standard.CONCAT(_logData, message), ShortenInstancePath(_instancePath), LogLevels.Information); 
  ELSE
    Tc2_System.ADSLOGSTR(ADSLOG_MSGTYPE_LOG OR LogLevelToAdsLogMsgType(LogLevels.Error), 'usedLogger: No valid reference', ''); 
  END_IF
END_IF
_logDataInitialized := FALSE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Init" Id="{9c7719d7-670f-46f3-92c8-88f67e360e00}">
      <Declaration><![CDATA[/// Initializes the reference to the core logger. Should only be called once from the core logger directly.
METHOD PUBLIC Init : BOOL
VAR_INPUT
  /// Reference to TcLoggerCore instance.
  ref2Core : ILogCore; 
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF s_loggerSingletonInstance = 0 THEN
	s_loggerSingletonInstance := ref2Core;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="InitializeLogData" Id="{b4d317d9-a916-4484-9cf0-3391543593aa}">
      <Declaration><![CDATA[// Necessary to provide a fluent interface of the function. 
// Has to be called at the begin of any method that returns a reference to THIS^
METHOD PRIVATE InitializeLogData : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT _logDataInitialized THEN
  _logConditionSatisfied := TRUE;
  _logData := '';
  _logDataInitialized := TRUE;
  IF _usedLogger = 0 THEN _usedLogger := s_loggerSingletonInstance; END_IF
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnCondition" Id="{a7892907-3646-4ffc-9ebc-1cb01c1fe466}">
      <Declaration><![CDATA[/// Conditional execution of the log command. Only executes logging when Cond = TRUE.
METHOD OnCondition : REFERENCE TO TcLog
VAR_INPUT
  /// Logs data only if this condition evaluates to TRUE.
  cond : BOOL; 
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[InitializeLogData();

_logConditionSatisfied := cond;

OnCondition REF= THIS^;]]></ST>
      </Implementation>
    </Method>
    <Method Name="SetLogger" Id="{46b10db2-eb0c-4fd5-9093-9efc1bae8abd}">
      <Declaration><![CDATA[/// Initializes the reference to the core logger. This enables you to use several
/// loggers with different configurations in one project.
METHOD SetLogger : BOOL
VAR_INPUT
  /// Reference to Core Logger
  ref2Core : ILogCore; 
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_usedLogger := ref2Core;]]></ST>
      </Implementation>
    </Method>
    <Method Name="ShortenInstancePath" Id="{b60d89ff-ac15-445d-94fa-fd767b6d287a}">
      <Declaration><![CDATA[// Helper method. Shortens the instance path by omitting superfluent information
// such as concrete logger instance and plc/project names.
{attribute 'hide'}
METHOD PRIVATE ShortenInstancePath : Tc2_System.T_MaxString
VAR_INPUT
  path : Tc2_System.T_MaxString; // Instance path to be shortened.
END_VAR
VAR
  i : UINT;
  shortenedPath : Tc2_System.T_MaxString;
  positionInString : INT := 0;
  positionOfLastDot : INT := 0;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[shortenedPath := path;

// Find last '.'
WHILE(Tc2_Standard.FIND(shortenedPath, '.') > 0) DO
  positionInString := Tc2_Standard.FIND(shortenedPath, '.');
  shortenedPath := Tc2_Standard.DELETE(shortenedPath, positionInString+1, 0); 
  positionOfLastDot := positionOfLastDot+positionInString;
END_WHILE

shortenedPath := Path;
// Delete instantiation of this FB (obvious information)
shortenedPath := Tc2_Standard.DELETE(shortenedPath, Tc2_Standard.LEN(shortenedPath)-positionOfLastDot+1, positionOfLastDot);
// Delete project information
positionInString := Tc2_Standard.FIND(shortenedPath, '.');
shortenedPath := Tc2_Standard.DELETE(shortenedPath, positionInString+1, 0);
// Delete PLC information
positionInString := Tc2_Standard.FIND(shortenedPath, '.');
shortenedPath := Tc2_Standard.DELETE(shortenedPath, positionInString+1, 0);

ShortenInstancePath := shortenedPath;]]></ST>
      </Implementation>
    </Method>
    <Method Name="ToAdsLog" Id="{5fabb9f2-d1d8-47fa-9304-f009c34b3048}">
      <Declaration><![CDATA[/// Logs directly to ADS log, ignoring the settings of the base logger.
METHOD ToAdsLog : BOOL
VAR_INPUT
  /// Log level in which message should be logged to ADS.
  logLevel : LogLevels; 
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[InitializeLogData();

IF _logConditionSatisfied THEN
  Tc2_System.ADSLOGSTR(ADSLOG_MSGTYPE_LOG OR LogLevelToAdsLogMsgType(logLevel), _logData, ''); 
END_IF

_logDataInitialized := FALSE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="ToCustomFormat" Id="{c0ece00e-4fc1-4a0f-829f-62251f71a82e}">
      <Declaration><![CDATA[/// Saves custom formatted log string. 
/// No addtitional information will be added, no formatting will be applied.
METHOD ToCustomFormat : BOOL
VAR_INPUT
  /// Message to be appended to log string.
  message : Tc2_System.T_MaxString; 
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[InitializeLogData();
IF _logConditionSatisfied THEN
  IF _usedLogger <> InvalidLoggerReference THEN 
    _usedLogger.LogCustomFormat(Tc2_Standard.CONCAT(_logData, message)); 
  ELSE
    Tc2_System.ADSLOGSTR(ADSLOG_MSGTYPE_LOG OR LogLevelToAdsLogMsgType(LogLevels.Error), 'usedLogger: No valid reference', ''); 
  END_IF
END_IF
_logDataInitialized := FALSE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Warning" Id="{162d314a-1aa9-4cae-884b-e8b1bb3f4d70}">
      <Declaration><![CDATA[/// Logs the log string in WARNING level.
METHOD Warning : BOOL
VAR_INPUT
  /// Message to be appended to log string.
  message : Tc2_System.T_MaxString; 
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[InitializeLogData();
IF _logConditionSatisfied THEN
  IF _usedLogger <> InvalidLoggerReference THEN 
    _usedLogger.LogStandardFormat(Tc2_Standard.CONCAT(_logData, message), ShortenInstancePath(_instancePath), LogLevels.Warning); 
  ELSE
    Tc2_System.ADSLOGSTR(ADSLOG_MSGTYPE_LOG OR LogLevelToAdsLogMsgType(LogLevels.Error), 'usedLogger: No valid reference', ''); 
  END_IF
END_IF
_logDataInitialized := FALSE;]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>